apply plugin: 'android'
apply plugin: 'crashlytics'
apply plugin: 'checkstyle'
apply plugin: 'pmd'

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
        packageName "eu.imind.android.imageupload"
        buildConfigField "boolean", "USE_CRASHLYTICS", "true"
    }

    productFlavors {
        staging {
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
        }

        daily {
            packageName = android.defaultConfig.packageName + ".daily"
            versionName = android.defaultConfig.versionName + "-DAILY"
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
        }
    }

    buildTypes {
        release {
            debuggable false
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }

        debug {
            packageNameSuffix '.debug'
            versionNameSuffix '-DEBUG'
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
        }
    }

    lintOptions {
        abortOnError false
        quiet true
        xmlReport true
        xmlOutput file("${project.buildDir}/reports/lint/lint.xml")
        htmlReport true
        htmlOutput file("${project.buildDir}/reports/lint/lint.html")
    }
}

dependencies {
    compile 'com.android.support:support-v4:+'
    compile 'com.android.support:appcompat-v7:+'
    compile 'com.crashlytics.android:crashlytics:1.+'
    compile 'com.jakewharton:butterknife:5.1.+'
    compile 'com.squareup.okhttp:okhttp:1.6.+'
    compile 'com.squareup.okhttp:okhttp-urlconnection:1.6.+'
    compile 'com.squareup.picasso:picasso:2.3.+'
    compile 'com.squareup:otto:1.3.+'
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

// *** Checkstyle config *** //
check.dependsOn 'checkstyle'
task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/code_analysis/config/checkstyle.xml")
    reports.xml.destination = file("${project.buildDir}/reports/checkstyle/checkstyle.xml")
    ignoreFailures = true
    showViolations = false
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

// *** PMD config *** //
check.dependsOn 'pmd'
task pmd (type: Pmd, dependsOn: assembleDebug) {
    group 'verification'
    ruleSetFiles = files("${project.rootDir}/code_analysis/config/pmd.xml")
    reports.xml.destination = file("${project.buildDir}/reports/pmd/pmd.xml")
    reports.html.destination = file("${project.buildDir}/reports/pmd/pmd.html")
    source = fileTree('src/main/java')
    ignoreFailures = true
    reports {
        xml.enabled = true
        html.enabled = true
    }
}